plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.1'
    id 'ru.vyarus.github-info' version '1.0.0'
    id 'ru.vyarus.animalsniffer' version '1.0.0'
    id 'ru.vyarus.quality' version '1.2.0'
    id 'com.github.kt3k.coveralls' version '2.4.0x'
    id 'com.jfrog.bintray' version '1.5'
    id 'net.researchgate.release' version '2.3.4'
    id 'com.github.ben-manes.versions' version '0.11.3'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

wrapper {
    gradleVersion = 2.10
}

ext {
    orient = '2.1.4'
    dropwizard = '0.8.4'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    signature 'org.codehaus.mojo.signature:java17:+@signature'

    provided 'org.webjars:orientdb-studio:2.1.0'

    compile "io.dropwizard:dropwizard-core:$dropwizard"
    compile "com.orientechnologies:orientdb-server:$orient"
    compile "com.orientechnologies:orientdb-tools:$orient"

    testCompile "io.dropwizard:dropwizard-testing:$dropwizard"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    // used to catch System.exit statements
    testCompile 'com.github.stefanbirkner:system-rules:1.9.0'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'

    // check everything works together
    // lucene includes graph dependency
    testCompile ("com.orientechnologies:orientdb-lucene:$orient") {
        exclude module: 'gremlin-java'
        exclude module: 'gremlin-groovy'
    }
    testCompile ("com.orientechnologies:orientdb-etl:$orient") {
        exclude module: 'gremlin-java'
        exclude module: 'gremlin-groovy'
    }
}

group = 'ru.vyarus'
description = 'Embedded OrientDB server for dropwizard'

github {
    user = 'xvik'
    license = 'MIT'
}

pom {
    developers {
        developer {
            id "xvik"
            name "Vyacheslav Rusakov"
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['dropwizard', 'orientdb']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standard_error"
        exceptionFormat "full"
    }
    maxHeapSize = "512m"
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') << {
    java.awt.Desktop.getDesktop().open(file('/build/reports/project/dependencies/root.html'))
}
